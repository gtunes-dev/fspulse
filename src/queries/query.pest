// fspulse_query.pest
// interactive parser: https://pest.rs/#editor

// Basic tokens - all silent
WHITESPACE = _{ " " | "\t" | "\n" }
COLON      = _{ ":" }
COMMA      = _{ "," }
DOTDOT     = _{ ".." }
LPAREN     = _{ "(" }
RPAREN     = _{ ")" }

// Top-level query: one of the domain-specific queries
query = { SOI ~ (items_query | scans_query | roots_query | changes_query | paths_query) ~ EOI }

where_ws = @{ "where" ~ WHITESPACE+ }

items_ws   = @{ "items" ~ WHITESPACE+ }
scans_ws   = @{ "scans" ~ WHITESPACE+ }
roots_ws   = @{ "roots" ~ WHITESPACE+ }
changes_ws = @{ "changes" ~ WHITESPACE+ }
paths_ws   = @{ "paths" ~ WHITESPACE+ }

items_where   = @{ items_ws ~ where_ws }
scans_where   = @{ scans_ws ~ where_ws }
roots_where   = @{ roots_ws ~ where_ws }
changes_where = @{ changes_ws ~ where_ws }
paths_where   = @{ paths_ws ~ where_ws }

items_query   = { items_where ~ fs_items }
scans_query   = { scans_where ~ fs_scans }
roots_query   = { roots_where ~ fs_roots }
changes_query = { changes_where ~ fs_changes }
paths_query   = { paths_where ~ fs_paths }

// Filter sets for each type (for items, we allow a COMMA-separated list of filters)
fs_items   = { fs_items_filter ~ (WHITESPACE* ~ COMMA ~ WHITESPACE* ~ fs_items_filter)* }
fs_scans   = { fs_scans_filter ~ (WHITESPACE* ~ COMMA ~ WHITESPACE* ~ fs_scans_filter)* }
fs_roots   = { fs_roots_filter ~ (WHITESPACE* ~ COMMA ~ WHITESPACE* ~ fs_roots_filter)* }
fs_changes = { fs_changes_filter ~ (WHITESPACE* ~ COMMA ~ WHITESPACE* ~ fs_changes_filter)* }
fs_paths   = { fs_paths_filter ~ (WHITESPACE* ~ COMMA ~ WHITESPACE* ~ fs_paths_filter)* }

// Allowable filters for the various types of queries
fs_items_filter   = { filter_change | filter_scan | filter_date | filter_validity }
fs_scans_filter   = { filter_scan | filter_date }
fs_roots_filter   = { WHITESPACE* }
fs_changes_filter = { filter_change | filter_validity }
fs_paths_filter   = { WHITESPACE* }

// Scan filter: e.g., scan:(32) or scan:(32..36)
filter_scan =  { "scan" ~ COLON ~ LPAREN ~ (scan_range | single_scan) ~ RPAREN }
scan_range  =  { single_scan ~ DOTDOT ~ single_scan }
single_scan = @{ ASCII_DIGIT+ }

// Date filter: e.g., date:(2023-01-01) or date:(2023-01-01..2023-01-31)
filter_date  =  { "date" ~ COLON ~ LPAREN ~ date_range ~ RPAREN }
date_range   =  { date_literal ~ (DOTDOT ~ date_literal)? }
date_literal = @{ ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} }

// Change filter: e.g., change:(A) or change:(A,M)
filter_change = { "change" ~ COLON ~ LPAREN ~ change_values ~ RPAREN }
change_values = { change_value ~ (WHITESPACE* ~ COMMA ~ WHITESPACE* ~ change_value)* }
change_value  = { "A" | "M" | "D" | "T" }

// Validity filter: e.g., validity:(V) or validity:(V,I)
filter_validity = { "validity" ~ COLON ~ LPAREN ~ validity_values ~ RPAREN }
validity_values = { validity_value ~ (WHITESPACE* ~ COMMA ~ WHITESPACE* ~ validity_value)* }
validity_value  = { "V" | "I" | "N" | "U" }